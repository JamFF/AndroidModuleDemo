if (isModule.toBoolean()) {// ① 切换
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'

def config = rootProject.ext// 定义变量
android {
    compileSdkVersion config.android.compileSdkVersion
    defaultConfig {
        if (isModule.toBoolean()) {// ② library没有applicationId
            applicationId "com.ff.modulea"
        }
        minSdkVersion config.android.minSdkVersion
        targetSdkVersion config.android.targetSdkVersion
        versionCode 1
        versionName "1.0"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            // ③ 加载不同位置的AndroidManifest
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    compileOptions {
        // ButterKnife 需要Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        // 禁用Google Search
        disable 'GoogleAppIndexingWarning'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    annotationProcessor "com.jakewharton:butterknife-compiler:$config.dependencies.butterknife"
    annotationProcessor "com.alibaba:arouter-compiler:$config.dependencies.arouter_compiler"
    api project(':baselib')
}
